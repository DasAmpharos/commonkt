plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.70" apply false
    id "info.solidsoft.pitest" version "1.4.0" apply false
    id "com.jfrog.artifactory" version "4.9.7" apply false
}

allprojects {
    group = "io.github.dylmeadows"
    version = "0.0.1-SNAPSHOT"

    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects {
    apply plugin: "kotlin"
    apply plugin: "java-library"
    apply plugin: "maven"
    apply plugin: "maven-publish"
    apply plugin: "com.jfrog.artifactory"
    apply plugin: "info.solidsoft.pitest"

    repositories {
        mavenCentral()
        jcenter()
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencies {
        implementation("org.jetbrains.kotlin:kotlin-stdlib")
        testImplementation("org.junit.jupiter:junit-jupiter:5.5.2")
        testImplementation("org.mockito:mockito-core:2.28.2")
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    task javadocJar(type: Jar) {
        dependsOn(javadoc)
        from javadoc.destinationDir
        archiveClassifier = "javadoc"
    }
    
    task sourcesJar(type: Jar) {
        dependsOn(classes)
        from sourceSets.main.allSource
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        archiveClassifier = "sources"

    }

    publishing {
        publications {
            mavenJavaLibrary(MavenPublication) {
                from components.java
            }
        }
    }

    artifactory {
        contextUrl = "https://oss.jfrog.org/artifactory"
        publish {
            repository {
                repoKey = "oss-snapshot-local"
                username = System.getenv("BINTRAY_USER")
                password = System.getenv("BINTRAY_KEY")
                maven = true
            }
            defaults {
                publications("mavenJavaLibrary")
                publishConfigs("archives", "published")
                publishArtifacts = true
                publishPom = true
            }
        }
        resolve {
            repository {
                repoKey = "libs-release"
                username = System.getenv("BINTRAY_USER")
                password = System.getenv("BINTRAY_KEY")
                maven = true
            }
        }
    }
}
